plugins {
	id "architectury-plugin" version "3.4-SNAPSHOT"
	id "dev.architectury.loom" version "1.3-SNAPSHOT" apply false
}

architectury {
	minecraft = minecraftVersion
}

subprojects {
	apply plugin: "dev.architectury.loom"

	loom {
		silentMojangMappingsLicense()

		runs.configureEach {
			vmArg("-Dmixin.debug.export=true")
			vmArg("-Dmixin.env.remapRefMap=true")
			vmArg("-Dmixin.env.refMapRemappingFile=${projectDir}/build/createSrgToMcp/output.srg")
		}
	}

	dependencies {
		minecraft "com.mojang:minecraft:${minecraftVersion}"

		mappings("net.fabricmc:yarn:${yarnVersion}:v2")

		modImplementation("com.google.code.findbugs:jsr305:${findBugsVersion}")

//		modApi "teamreborn:energy:${energyVersion}"
	}
}

allprojects {
	apply plugin: "java"
	apply plugin: "architectury-plugin"
	apply plugin: "maven-publish"

	group = projectGroup
	version = projectVersion

	base {
		archivesName = rootProject.projectId
	}

	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"
		options.release = 17
	}

	java {
		withSourcesJar()
	}

	repositories {
		maven { url "https://maven.vini2003.dev/releases" }
		maven { url "https://maven.fabricmc.net/" }
		maven { url "https://maven.enginehub.org/repo/" }
	}
}